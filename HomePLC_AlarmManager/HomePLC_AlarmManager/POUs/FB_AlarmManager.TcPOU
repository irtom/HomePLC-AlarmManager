<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="FB_AlarmManager" Id="{6bd1ff22-13a9-41fc-b80e-e488afffffbb}" SpecialFunc="None">
    <Declaration><![CDATA[// Alarm manager
{attribute 'reflection'} 
FUNCTION_BLOCK FB_AlarmManager IMPLEMENTS I_AlarmManager
VAR
	aKeypads				: ARRAY [0..GVL_AlarmManager.nMaxKeypads - 1] OF I_AlarmKeypad;		// Keypads
	nKeypads				: INT;																// Number of keypads
	
	aSensors				: ARRAY [0..GVL_AlarmManager.nMaxSensors - 1] OF I_AlarmSensor;		// Alarm sensors
	nSensors				: INT;																// Number of alarm sensors
	
	aSirens					: ARRAY [0..GVL_AlarmManager.nMaxSirens - 1] OF I_AlarmSiren;		// Alarm sirens
	nSirens					: INT;																// Number of alarm sirens
	
	eState					: E_AlarmState;														// Alarm state
	
	bEnabling		AT %Q*	: BOOL;																// Alarm enabling (can be read externally)
	bEnabled		AT %Q*	: BOOL;																// Alarm enabled (can be read externally)
	nState			AT %Q*	: INT;																// Alarm state number (can be read externally)
	nActiveID		AT %Q*	: INT;																// Alarm component ID that tripped alarm (can be read externally)
	nActiveReason	AT %Q*	: INT;																// ID reason alarm was tripped (can be read externally)
	
	fbLogger				: class_Tc3EventLogger;												// Logging FB

	// Use attributes to copy instance name to sName
	{attribute 'instance-path'} 
    {attribute 'noinit'} 
    sName					: STRING; 															// Instance name
	
	stHMI					: ST_AlarmManagerHMI;												// HMI data
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Folder Name="Methods" Id="{6b8c8bcd-cc3f-41d5-91c5-467ed078760c}" />
    <Method Name="AddKeypad" Id="{714db9aa-f5e5-4391-8e31-d6cad9bd9b6e}" FolderPath="Methods\">
      <Declaration><![CDATA[// Add a keypad to the alarm manager
METHOD AddKeypad
VAR_INPUT
	IKeypad		: I_AlarmKeypad;		// Keypad interface
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Add keypad
aKeypads[nKeypads] := IKeypad;
nKeypads := nKeypads + 1;]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddSensor" Id="{bb72c9a0-6888-40a1-881d-6ef7ae3a27c5}" FolderPath="Methods\">
      <Declaration><![CDATA[// Add an alarm sensor to the alarm manager
METHOD AddSensor
VAR_INPUT
	ISensor		: I_AlarmSensor;		// Alarm sensor interface
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Add alarm sensor
aSensors[nSensors] := ISensor;
nSensors := nSensors + 1;]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddSiren" Id="{a27aff50-53ad-4cb3-a598-24c213c802a8}" FolderPath="Methods\">
      <Declaration><![CDATA[// Add a siren to the alarm manager
METHOD AddSiren
VAR_INPUT
	ISiren		: I_AlarmSiren;		// Siren interface
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Add siren
aSirens[nSirens] := ISiren;
nSirens := nSirens + 1;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Execute" Id="{ee2098df-6b45-4b5c-a6b0-e303f06166b9}" FolderPath="Methods\">
      <Declaration><![CDATA[// Run this method every cycle
METHOD Execute
VAR_INST
	eStep						: E_AlarmManagerStep;	// Alarm manager state machine step
	eStepPrev					: E_AlarmManagerStep;	// Alarm manager state machine previous step
	bFirstCall					: BOOL;					// Alarm manager state machine step first call
	
	bCodeEntered				: BOOL;					// True if the configured code was entered on a keypad
	bSoundBuzzer				: BOOL;					// True if the buzzer needs to sound to indicate the alarm status
	bEnablingBuzzer				: BOOL;					// True if the buzzer needs to sound when enabling the alarm
	bResetSensors				: BOOL;					// True if the sensor disable states need to be reset
	bSensorTriggered			: BOOL;					// True if a sensor is triggered
	bSensorTampered				: BOOL;					// True if a sensor is tampered with
	bWarn						: BOOL;					// True if the user needs to be warned first when a sensor is triggered
	aOutputOnDetect				: ARRAY [0..9] OF I_DigitalOut;	// Returns the outputs to turn on when the sensor is triggered
	nOutputOnDetect				: INT;					// Returns the number of outputs in aOutputOnDetect
	bSirenTampered				: BOOL;					// True if a siren is tampered with
	nActiveSensorID				: INT;					// Alarm component ID of activated sensor
	nActiveSirenID				: INT;					// Alarm component ID of tampered siren
	
	eBuzzerCommand				: E_DOCommand;			// Keypad buzzer digital output command
	eSirenLightCommand			: E_DOCommand;			// Siren light digital output command
	eSirenSoundCommand			: E_DOCommand;			// Siren sound digital output command
	
	fbTimer						: TON;					// Timer FB
	
	sLog						: STRING;				// Log string
END_VAR
VAR
	i							: INT;					// Iterator variable
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Execute methods
Execute_Keypads(eBuzzerCommand := eBuzzerCommand,
				bCodeEntered => bCodeEntered,
				bSoundBuzzer => bSoundBuzzer);
Execute_Sensors(bResetDisableStates := bResetSensors,
				bSensorTriggered => bSensorTriggered,
				bSensorTampered => bSensorTampered,
				bWarn => bWarn,
				nActiveSensorID => nActiveSensorID,
				oaOutputOnDetect => aOutputOnDetect,
				onOutputOnDetect => nOutputOnDetect);
Execute_Sirens(eLightCommand := eSirenLightCommand,
				eSoundCommand := eSirenSoundCommand,
				bSirenTampered => bSirenTampered,
				nActiveSirenID => nActiveSirenID);

// Set variables
bFirstCall := eStep <> eStepPrev;
eStepPrev := eStep;

// Execute alarm logic
CASE eStep OF
	E_AlarmManagerStep.Init:
		// Initialize
		eStep := E_AlarmManagerStep.CheckEnable;
		
	E_AlarmManagerStep.CheckEnable:
		// Check if configured code is entered by the user
		IF bCodeEntered THEN
			// Reset variables
			nActiveID := 0;
			nActiveReason := 0;
			
			// Reset sensor disabled states
			bResetSensors := TRUE;
			
			// Save whether to sound buzzer during enabling
			bEnablingBuzzer := bSoundBuzzer;
			
			eStep := E_AlarmManagerStep.HardwareCheck;
		END_IF
		
	E_AlarmManagerStep.HardwareCheck:
		// Hardware check before enabling alarm
		
		// Check if a sensor detects something or if one is tampered with
		// Check if a siren is tampered with
		IF bSensorTampered OR bSirenTampered THEN
			// Sound buzzer to indicate alarm cannot be enabled
			eBuzzerCommand := E_DOCommand.Flash;
			
			fbLogger.AddInfo('Alarm cannot be enabled due to tampering');
			
			eStep := E_AlarmManagerStep.Reset;
		ELSE
			eStep := E_AlarmManagerStep.Enabling;
		END_IF
		
	E_AlarmManagerStep.Enabling:
		// Enabling alarm
		IF bFirstCall THEN
			// Initialize timer
			fbTimer.PT := T#20S;
			fbTimer(IN := FALSE);
			
			// Set alarm state
			eState := E_AlarmState.Enabling;
			
			// Check if the buzzer needs to be used when enabling the alarm
			IF bEnablingBuzzer THEN
				// Set buzzer to blinking
				eBuzzerCommand := E_DOCommand.Blink;
			END_IF
		ELSE
			// Run timer FB
			fbTimer(IN := TRUE);
			
			// Check if timer expired
			IF fbTimer.Q THEN
				// Turn off buzzer
				eBuzzerCommand := E_DOCommand.Off;
			
				eStep := E_AlarmManagerStep.Enable;
			END_IF
		END_IF
		
		// Check if configure code is entered by user
		IF bCodeEntered THEN
			eStep := E_AlarmManagerStep.Disabling;
		END_IF
	
	E_AlarmManagerStep.Enable:
		// Enable alarm
		eState := E_AlarmState.Enabled;
		
		fbLogger.AddInfo('Alarm enabled');
		
		eStep := E_AlarmManagerStep.CheckSensors;
		
	E_AlarmManagerStep.CheckSensors:
		// Check if configure code is entered by user
		IF bCodeEntered THEN
			eStep := E_AlarmManagerStep.Disabling;
		END_IF
		
		// Check if a sensor or siren is tampered with
		IF bSensorTampered OR bSirenTampered THEN
			// Save what tripped alarm
			nActiveReason := 1;		// Tampering
			
			IF bSensorTampered THEN
				nActiveID := nActiveSensorID;
			END_IF
			
			IF bSirenTampered THEN
				nActiveID := nActiveSirenID;
			END_IF
			
			sLog := CONCAT('Alarm tripped because of tampering by ID ', TO_STRING(nActiveID));
			fbLogger.AddWarning(sLog);
			
			eStep := E_AlarmManagerStep.SoundAlarm;
		END_IF
		
		// Check if a sensor detects something
		IF bSensorTriggered THEN
			// Save sensor name
			nActiveID := nActiveSensorID;
			nActiveReason := 2;		// Motion
			
			sLog := CONCAT('Alarm tripped because of motion by ID ', TO_STRING(nActiveID));
			fbLogger.AddWarning(sLog);
			
			// Check if user needs to be warned
			IF bWarn THEN
				eStep := E_AlarmManagerStep.Warn;
			ELSE				
				eStep := E_AlarmManagerStep.SoundAlarm;
			END_IF
		END_IF
		
	E_AlarmManagerStep.Warn:
		// Warn the user before sounding the alarm
		eBuzzerCommand := E_DOCommand.On;
		
		// Set alarm state
		eState := E_AlarmState.Warning;
		
		fbLogger.AddInfo('Sensor triggered, warning user');
		
		eStep := E_AlarmManagerStep.WaitForDisableWarn;
		
	E_AlarmManagerStep.WaitForDisableWarn:
		// Wait for user to disable alarm and sound the alarm if it does not happen in time
		IF bFirstCall THEN
			// Initialize timer
			fbTimer.PT := T#30S;
			fbTimer(IN := FALSE);
		ELSE
			// Run timer FB
			fbTimer(IN := TRUE);
			
			// Check if configured code is entered by user
			IF bCodeEntered THEN
				// Disable alarm
				eStep := E_AlarmManagerStep.Disabling;
			END_IF
			
			// Check if timer expired
			IF fbTimer.Q THEN
				// Sound alarm
				eStep := E_AlarmManagerStep.SoundAlarm;
			END_IF
		END_IF
		
	E_AlarmManagerStep.SoundAlarm:
		// Turn on alarm sound
		eSirenSoundCommand := E_DOCommand.On;
		
		// Set alarm state
		eState := E_AlarmState.Tripped;
		
		// Turn off buzzer (if on)
		eBuzzerCommand := E_DOCommand.Off;
		
		// Turn on digital outputs associated with sensor that was triggered
		IF nOutputOnDetect > 0 THEN
			FOR i := 0 TO nOutputOnDetect - 1 DO
				aOutputOnDetect[i].SetCommand(eCommandIn := E_DOCommand.On);
			END_FOR
		END_IF
		
		fbLogger.AddInfo('Sounding alarm');
		
		eStep := E_AlarmManagerStep.WaitForDisableAlarm;
		
	E_AlarmManagerStep.WaitForDisableAlarm:
		// Wait for user to disable alarm or wait some time before turning it off automatically
		IF bFirstCall THEN
			// Initialize timer
			fbTimer.PT := T#3M;
			fbTimer(IN := FALSE);
		ELSE
			// Run timer FB
			fbTimer(IN := TRUE);
			
			// Check if configured code is entered by user or if timer expired
			IF bCodeEntered OR fbTimer.Q THEN
				// Disable alarm
				eStep := E_AlarmManagerStep.Disabling;
			END_IF
		END_IF
		
	E_AlarmManagerStep.Disabling:
		IF bFirstCall THEN
			// Turn off alarm sound
			eSirenSoundCommand := E_DOCommand.Off;
		
			// Turn off buzzer (if on)
			eBuzzerCommand := E_DOCommand.Off;
			
			// Set alarm status
			eState := E_AlarmState.Disabling;
			
			// Initialize timer
			fbTimer.PT := T#1.5S;
			fbTimer(IN := FALSE);
		ELSE
			// Run timer FB
			fbTimer(IN := TRUE);
			
			// Check if timer expired
			IF fbTimer.Q THEN
				// Disable alarm
				eStep := E_AlarmManagerStep.Disable;
			END_IF
		END_IF
		
	E_AlarmManagerStep.Disable:
		// Set alarm status
		eState := E_AlarmState.Off;
		
		fbLogger.AddInfo('Alarm disabled');
		
		eStep := E_AlarmManagerStep.Reset;
		
	E_AlarmManagerStep.Reset:
		eStep := E_AlarmManagerStep.CheckEnable;
		
END_CASE

// Update variables
nState := eState;
bEnabling := eState = E_AlarmState.Enabling;
bEnabled := eState = E_AlarmState.Enabled;

// Update HMI
UpdateHMI();]]></ST>
      </Implementation>
    </Method>
    <Method Name="Execute_Keypads" Id="{6c4a40d7-e936-4d95-b80b-468ee9234db6}" FolderPath="Methods\">
      <Declaration><![CDATA[// Run this method every cycle
METHOD PRIVATE Execute_Keypads
VAR_INPUT
	eBuzzerCommand			: E_DOCommand;	// Keypad buzzer command
END_VAR
VAR_OUTPUT
	bCodeEntered			: BOOL;			// Returns true if the configured code is entered by the user
	bSoundBuzzer			: BOOL;			// Returns true if the buzzer needs to sound to indicate the alarm status
END_VAR
VAR_INST
	bInit					: BOOL;			// Initialized flag
	eBuzzerCommandPrev		: E_DOCommand;	// Previous buzzer digital output command
	fbBuzzerCommandTrigger	: R_TRIG;		// Buzzer command trigger
	eStatePrev				: E_AlarmState;	// Previous alarm state
	fbStateTrigger			: R_TRIG;		// Alarm state change trigger
END_VAR
VAR
	i						: INT;			// Iterator variable
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Check if buzzer command changed
fbBuzzerCommandTrigger(CLK := eBuzzerCommand <> eBuzzerCommandPrev);
eBuzzerCommandPrev := eBuzzerCommand;

// Check if alarm state changed
fbStateTrigger(CLK := eState <> eStatePrev);
eStatePrev := eState;

// Initialize variable
bCodeEntered := FALSE;

FOR i := 0 TO nKeypads - 1 DO	
	// Set keypad configure code
	aKeypads[i].SetConfiguredCode(sCode := GVL_AlarmManager.sCode);
	
	// Check if buzzer command changed
	IF fbBuzzerCommandTrigger.Q THEN
		// Set buzzer digital output command
		aKeypads[i].SetBuzzer(eCommand := eBuzzerCommand);
	END_IF
	
	// Check if alarm state changed
	IF fbStateTrigger.Q OR NOT bInit THEN
		// Update keypad on alarm state change
		aKeypads[i].SetAlarmState(eAlarmState := eState);
	END_IF
	
	// Run keypad Execute method
	aKeypads[i].Execute();
	
	// Check if the configured code is entered on at least one keypad
	IF aKeypads[i].CheckCode() THEN
		bCodeEntered := TRUE;
		bSoundBuzzer := aKeypads[i].SoundBuzzer;
	END_IF
END_FOR

bInit := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Execute_Sensors" Id="{b6779498-0c72-4d8e-84f1-99dfdd8c538e}" FolderPath="Methods\">
      <Declaration><![CDATA[// Run this method every cycle
METHOD PRIVATE Execute_Sensors
VAR_INPUT
	bResetDisableStates	: BOOL;					// Set to true to reset all sensor disabled states
END_VAR
VAR_OUTPUT
	bSensorTriggered	: BOOL;					// Returns true if a sensor detects something
	bSensorTampered		: BOOL;					// Returns true if a sensor is tampered with
	bWarn				: BOOL;					// Returns true if a warning is issued first instead of immediately sounding the alarm (only valid on sensor trigger)
	nActiveSensorID		: INT;					// Returns the ID of the active sensor
	oaOutputOnDetect	: ARRAY [0..9] OF I_DigitalOut;	// Returns the outputs to turn on when the sensor is triggered
	onOutputOnDetect	: INT;					// Returns the number of outputs in aOutputOnDetect
END_VAR
VAR_INST
	fbResetTrigger		: R_TRIG;				// Reset disabled state trigger
	aOutputOnDetect		: ARRAY [0..9] OF I_DigitalOut;	// Returns the outputs to turn on when the sensor is triggered
	nOutputOnDetect		: INT;					// Returns the number of outputs in aOutputOnDetect
END_VAR
VAR
	i					: INT;					// Iterator variable
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Initialize variables
bSensorTriggered := FALSE;
bSensorTampered	:= FALSE;
bWarn := TRUE;

// Check reset command
fbResetTrigger(CLK := bResetDisableStates);

FOR i := 0 TO nSensors -1 DO
	// Reset disables states
	IF fbResetTrigger.Q THEN
		aSensors[i].SetDisableState(bDisable := FALSE);
	END_IF

	// Run sensor Execute method
	aSensors[i].Execute();

	// Check if at least one sensor detects something or is tampered with
	bSensorTriggered := bSensorTriggered OR aSensors[i].AlarmTrigger;
	bSensorTampered := bSensorTampered OR aSensors[i].TamperTrigger;
	
	// Check if alarm needs to be sounded immediately or if a warning can be issued first
	IF aSensors[i].AlarmTrigger AND NOT aSensors[i].TamperTrigger THEN
		// Sensor detects something but is not tampered with
		// Warn if the sensor allows a warning
		// If one sensor does not allow a warning, sound the alarm immediately
		bWarn := bWarn AND aSensors[i].WarnFirst;
	END_IF
	
	// Report which sensor was activated
	IF aSensors[i].AlarmTrigger OR aSensors[i].TamperTrigger THEN
		IF nActiveSensorID = 0 THEN
			nActiveSensorID := aSensors[i].ID;
			aOutputOnDetect := aSensors[i].OutputsOnDetect;
			nOutputOnDetect := aSensors[i].OutputsOnDetectSize;
		END_IF
	END_IF
END_FOR

// Set outputs
oaOutputOnDetect := aOutputOnDetect;
onOutputOnDetect := nOutputOnDetect;]]></ST>
      </Implementation>
    </Method>
    <Method Name="Execute_Sirens" Id="{dce3f125-fe98-4063-95eb-97d55a76db83}" FolderPath="Methods\">
      <Declaration><![CDATA[// Run this method every cycle
METHOD PRIVATE Execute_Sirens
VAR_INPUT
	eLightCommand				: E_DOCommand;		// Siren light digital output command
	eSoundCommand				: E_DOCommand;		// Siren sound digital output command
END_VAR
VAR_OUTPUT
	bSirenTampered				: BOOL;				// Returns true if the siren is tampered with
	nActiveSirenID				: INT;				// Returns the name of the tampered siren
END_VAR
VAR
	i							: INT;				// Iterator variable
END_VAR
VAR_INST
	eLightCommandPrev			: E_DOCommand;		// Siren light previous digital output command
	fbLightCommandTrigger		: R_TRIG;			// Siren light command changed trigger
	
	eSoundCommandPrev			: E_DOCommand;		// Siren sound previous digital output command
	fbSoundCommandTrigger		: R_TRIG;			// Siren sound command changed trigger
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Check if the siren light digital command has changed
fbLightCommandTrigger(CLK := eLightCommand <> eLightCommandPrev);
eLightCommandPrev := eLightCommand;

// Check if the siren sound digital command has changed
fbSoundCommandTrigger(CLK := eSoundCommand <> eSoundCommandPrev);
eSoundCommandPrev := eSoundCommand;

// Initialize variable
bSirenTampered	:= FALSE;

FOR i := 0 TO nSirens - 1 DO
	// Check if siren light command changed
	IF fbLightCommandTrigger.Q THEN
		// Set siren light digital output command
		aSirens[i].SetLight(eCommand := eLightCommand);
	END_IF
	
	// Check if siren sound command changed
	IF fbSoundCommandTrigger.Q THEN
		// Set siren sound digital output command
		aSirens[i].SetSound(eCommand := eSoundCommand);
	END_IF
	
	// Run siren Execute method
	aSirens[i].Execute();
	
	// Check if at least one siren is tampered with
	bSirenTampered := bSirenTampered OR aSirens[i].TamperTrigger;
	
	// Report which siren was tampered with
	IF aSirens[i].TamperTrigger THEN
		IF nActiveSirenID = 0 THEN
			nActiveSirenID := aSirens[i].ID;
		END_IF
	END_IF
END_FOR]]></ST>
      </Implementation>
    </Method>
    <Method Name="FB_init" Id="{642bd196-ed73-4737-be4b-f806cdc027db}" FolderPath="Methods\">
      <Declaration><![CDATA[// Initialize the alarm manager FB
METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Initialize logger
fbLogger.Init(sSource := sName, bEnableVerbose := TRUE);]]></ST>
      </Implementation>
    </Method>
    <Method Name="UpdateHMI" Id="{b53f5690-2279-4425-9271-96dee2af7c37}" FolderPath="Methods\">
      <Declaration><![CDATA[// Update HMI
METHOD PRIVATE UpdateHMI
VAR
	i	: INT;	// Iterator variable
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// General
stHMI.nKeypads := nKeypads;
stHMI.nSensors := nSensors;
stHMI.nSirens := nSirens;
stHMI.eState := eState;
stHMI.nActiveID := nActiveID;
stHMI.nActiveReason := nActiveReason;

// Overview
FOR i := 0 TO nSensors - 1 DO
	stHMI.aSensors[i] := aSensors[i].HmiData;
END_FOR

FOR i := 0 TO nSirens - 1 DO
	stHMI.aSirens[i] := aSirens[i].HmiData;
END_FOR]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>